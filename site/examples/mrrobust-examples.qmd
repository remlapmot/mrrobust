---
title: Examples from helpfiles in the mrrobust package
---

```{r}
#| include: false
knitr::opts_chunk$set(
    fig.align = "center"
)
```

```{r}
#| include: false
library(Statamarkdown)
```

```{stata, include=FALSE, collectcode=TRUE}
adopath ++ ..
```

## Install the mrrobust package
```stata
net install github, from("https://haghish.github.io/github/")
gitget mrrobust
```

## Read in example data

Read in example data.
```{stata collectcode=TRUE, results='hide'}
use https://raw.github.com/remlapmot/mrrobust/master/dodata, clear
```

Select observations (*p*-value with exposure < 10^-8^).
```{stata collectcode=TRUE, results='hide'}
gen byte sel1 = (ldlcp2 < 1e-8)
```

## mrforest examples
Forest plot of genotype specific IV estimates and IVW and MR-Egger estimates, 
labelling the genotypes with their RSID.
```{stata}
mrforest chdbeta chdse ldlcbeta ldlcse if sel1==1, ///
	ivid(rsid) ///
	xlabel(-5,-4,-3,-2,-1,0,1,2,3,4,5)
qui gr export mrforest.svg, width(600) replace
```

{:style="text-align:center;"}
![Example forest plot of genotype specific IV estimates.](mrforest.svg)

## mregger examples
Using the data provided by [Do et al. (2013)](https://doi.org/10.1038/ng.2795) 
recreate [Bowden et al. (2016)](https://doi.org/10.1002/gepi.21965), 
Table 4, LDL-c "All genetic variants" estimates.

IVW (with fixed effect standard errors, i.e. variance of residuals 
[residual variance] constrained to 1).
```{stata}
mregger chdbeta ldlcbeta [aw=1/(chdse^2)] if sel1==1, ivw fe
```

MR-Egger (with SEs using an unconstrained residual variance 
[multiplicative random effects]).
```{stata}
mregger chdbeta ldlcbeta [aw=1/(chdse^2)] if sel1==1
```

MR-Egger reporting I^2_GX statistic and heterogeneity Q-test.
```{stata}
mregger chdbeta ldlcbeta [aw=1/(chdse^2)] if sel1==1, ///
	gxse(ldlcse) heterogi
```

MR-Egger using a t-distribution for inference (p-values) & CI limits.
```{stata}
mregger chdbeta ldlcbeta [aw=1/(chdse^2)] if sel1==1, tdist
```

MR-Egger using the radial formulation.
```{stata}
mregger chdbeta ldlcbeta [aw=1/(chdse^2)] if sel1==1, radial
```

MR-Egger using the radial formulation and reporting heterogeneity (Rucker's) Q-test.
```{stata}
mregger chdbeta ldlcbeta [aw=1/(chdse^2)] if sel1==1, ///
	radial heterogi
```

## mreggersimex examples
SIMEX suppressing bootstrapped SEs (for speed - remove `noboot` option to obtain 
SEs).
```{stata}
mreggersimex chdbeta ldlcbeta [aw=1/chdse^2] if sel1==1, ///
	gxse(ldlcse) seed(12345) noboot
qui gr export mreggersimex-plot.svg, width(600) replace
```

{:style="text-align:center;"}
![SIMEX plots for the MR-Egger model.](mreggersimex-plot.svg)

## mreggerplot examples
```{stata}
mreggerplot chdbeta chdse ldlcbeta ldlcse if sel1==1
qui gr export mreggerplot.svg, width(600) replace
```

{:style="text-align:center;"}
![Scatter plot showing the genotype summary level data points and the fitted MR-Egger model.](mreggerplot.svg)

## mrmedian examples
Weighted median estimator.
```{stata}
mrmedian chdbeta chdse ldlcbeta ldlcse if sel1==1, weighted
```

## mrmodal examples
```{stata}
mrmodalplot chdbeta chdse ldlcbeta ldlcse if sel1==1, seed(12345)
qui gr export mrmodalplot.svg, width(600) replace
```

{:style="text-align:center;"}
![Densities of the IV estimates using different values of the phi parameter.](mrmodalplot.svg)

Simple mode estimator.
```{stata}
mrmodal chdbeta chdse ldlcbeta ldlcse if sel1==1
```

Weighted mode estimator.
```{stata}
mrmodal chdbeta chdse ldlcbeta ldlcse if sel1==1, weighted
```

Simple mode estimator with NOME assumption.
```{stata}
mrmodal chdbeta chdse ldlcbeta ldlcse if sel1==1, nome
```

## mrfunnel examples
```{stata}
mrfunnel chdbeta chdse ldlcbeta ldlcse if sel1==1, xlrange(0 10)
qui gr export mrfunnel.svg, width(600) replace
```

{:style="text-align:center;"}
![Example funnel plot.](mrfunnel.svg)

## Leave one out analysis
Create a smaller subset of data.
```{stata}
gen byte sel2 = (ldlcp2 < 1e-25)
```

Perform leave one out analysis using the IVW estimator
```{stata}
mrleaveoneout chdbeta ldlcbeta if sel2==1, gyse(chdse) genotype(rsid) noprint
qui gr export mrleaveoneout-plot-01.svg, width(600) replace
```

{:style="text-align:center;"}
![Plot showing leave one (genotype) out analysis for the IVW estimate.](mrleaveoneout-plot-01.svg)

Perform leave one out analysis using MVMR collecting the estimate for LDL-c.
```{stata}
mrleaveoneout chdbeta ldlcbeta hdlcbeta tgbeta if sel2==1, ///
	method(mvmr) gyse(chdse) genotype(rsid) noprint
qui gr export mrleaveoneout-plot-02.svg, width(600) replace
```

{:style="text-align:center;"}
![Plot showing leave one out analysis for the MVMR estimate.](mrleaveoneout-plot-02.svg)
